plugins {
    id 'java'
    id 'idea'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

javafx {
    modules = ['javafx.controls', 'javafx.fxml']
}

javafx {
    version = "11.0.1"
    configuration = "compileOnly"
}

version '2.3.0'
group 'com.edinarobotics.plugin'

repositories {
    maven {
        url 'https://plugins.gradle.org/m2/'
    }
    maven { url "https://jitpack.io" }
    maven { url "https://frcmaven.wpi.edu/artifactory/release/" }
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

def shuffleboardVersion = '2020.2.2'

dependencies {
    compileOnly group: "edu.wpi.first.shuffleboard", name: "api", version: shuffleboardVersion
    compileOnly group: 'edu.wpi.first.shuffleboard.plugin', name: 'networktables', version: shuffleboardVersion
    // https://mvnrepository.com/artifact/org.freedesktop.gstreamer/gst1-java-core
    // https://mvnrepository.com/artifact/org.freedesktop.gstreamer/gst1-java-fx
    implementation group: 'org.freedesktop.gstreamer', name: 'gst1-java-fx', version: '0.9.0'
}

jar {
    manifest {
        attributes 'Main-Class' : ''
    }
    from {
        configurations.runtimeClasspath.collect() { it.isDirectory() ? it : zipTree(it) }
    }
    exclude('Main')
    exclude('META-INF/*')
}

wrapper {
    gradleVersion = '6.7'
}

task installPlugin(type: Copy) {
    from(tasks.named("jar"))
    into("${System.getProperty("user.home")}/Shuffleboard/plugins")
    description = "Builds the plugin JAR and installs it in the Shuffleboard plugins directory."
    group = "Shuffleboard Plugin"
}

// https://mvnrepository.com/artifact/org.freedesktop.gstreamer/gst1-java-fx
